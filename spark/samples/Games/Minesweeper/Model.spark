model Minesweeper

space GridSpace -10 10 -10 10 false false

@parameter(default = 40, min = 0, max = 200)
global bombs : number


global state : number


to open-cell [ p : vector ]
	var cells = agents-at Cell p 0.1
	ask cells 
	[
		open
	]
end

to flag-cell [ p : vector ]
	var cells = agents-at Cell p 0.1
	ask cells
	[
		flag
	]
end


to begin-step
	while true
	[
		var e = next-mouse-event
		if e == null 
			[ break ]
		
		if e.event-type == "LBUTTON_UP"
			[ open-cell e.position ]
			
		if e.event-type == "RBUTTON_DOWN"
			[ flag-cell e.position ]
	]
end


to create-cells
	for x = space-xmin : space-xmax - 1
	[
		for y = space-ymin : space-ymax - 1
		[
			var p = create-vector (x + 0.5) (y + 0.5) 0
			
			ask create-one Cell
			[
				move-to p
			]
		]
	]
end


to random-empty-spot : vector
	repeat 1000
	[
		var x = floor (random-in-interval (space-xmin + 1) (space-xmax - 1))
		var y = floor (random-in-interval (space-ymin + 1) (space-ymax - 1))
	
		var p = create-vector (x + 0.5) (y + 0.5) 0
		if count agents-at Bomb p 0.1 == 0
			[ return p ]
	]
	
	return [0,0,0]
end


to create-bombs
	repeat bombs
	[
		ask create-one Bomb
		[
			move-to this.random-empty-spot
		]
	]
end


to setup
	state = 0

	create-cells
	create-bombs
end
